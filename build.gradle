// tasks: spotbugsMain, spotbugsTest, spotbugsIntegrationTest or "check" for all
plugins {
	id 'java-library'
	id "io.spring.dependency-management" version "1.1.7"
	// spotbugs not yet jdk 25 compatible
    // id "com.github.spotbugs" version "6.4.2"
	// ./gradlew dependencyUpdates
	id "com.github.ben-manes.versions" version "0.52.0"
	id 'com.github.node-gradle.node' version '5.0.0'
	id 'war'
	id "org.springframework.boot" version "3.5.5"
	id "checkstyle"
	// run jacoco via gradle jacocoTestReport, report in build/reports
	id 'jacoco'
}

group = 'org.tightblog'
version = '4.1.4'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
}

sourceSets {
	test {
		java {
			srcDir 'src/test/java'
		}
	}
	// https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integrationTest/java')
		}
		resources.srcDir file('src/integrationTest/resources')
	}
}

static def standardCompile(options) {
    options.compilerArgs += ["-proc:none", "-Xlint:unchecked"]
}

compileJava {
    standardCompile(options)
}

compileTestJava {
    standardCompile(options)
}

compileIntegrationTestJava {
    standardCompile(options)
}

project.ext.buildNumber = java.time.Instant.now().getEpochSecond()

def standardProcessResources(obj) {
    obj.filesMatching(['application.properties', 'application-tbcustom.properties']) {
        expand(version: version,
               buildNumber: buildNumber,
               buildDir: buildDir
        )
    }
}

processResources {
    standardProcessResources(processResources)
}

processTestResources {
    standardProcessResources(processTestResources)
}

processIntegrationTestResources {
    standardProcessResources(processIntegrationTestResources)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

configurations {
	integrationTestImplementation.extendsFrom implementation, testImplementation
	integrationTestRuntimeOnly.extendsFrom runtimeOnly
	// using log4j2 instead of logback
	// global excludes: http://mrhaki.blogspot.com/2012/10/gradle-goodness-exclude-transitive.html
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

tasks.register('integrationTest', Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	mustRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest

/* spotbugs not yet jdk 25 compatible
spotbugs {
	excludeFilter = file("$rootProject.projectDir/etc/buildconfig/spotbugsExcludes.xml")
}
*/

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-mail')
	implementation('org.springframework.boot:spring-boot-starter-log4j2')
	implementation('org.springframework.boot:spring-boot-starter-validation')
	implementation('org.flywaydb:flyway-core')
	implementation('org.flywaydb:flyway-mysql')
	implementation('org.springframework.security:spring-security-taglibs')
	implementation('org.apache.commons:commons-lang3:3.18.0')
	implementation('org.apache.commons:commons-text:1.10.0')
	implementation('org.apache.lucene:lucene-analyzers-common:8.10.0')
	implementation('org.apache.lucene:lucene-queryparser:8.10.0')
	implementation('org.apache.lucene:lucene-backward-codecs:8.10.0')
	implementation("jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.2")
	implementation("org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1")
	implementation('commons-validator:commons-validator:1.7')
	implementation('org.jsoup:jsoup:1.16.1')
	implementation('com.github.ben-manes.caffeine:caffeine:3.1.8')
	implementation('org.jboss.aerogear:aerogear-otp-java:1.0.0')
	implementation('com.atlassian.commonmark:commonmark:0.17.0')
	implementation('commons-fileupload:commons-fileupload:1.6.0')
	implementation("io.nayuki:qrcodegen:1.8.0")
	// Jackson Hibernate module to support lazy loading
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6:2.19.2'


	testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.junit.jupiter:junit-jupiter:5.10.2')

    // MySQL JDBC driver for Docker deployments
    runtimeOnly('com.mysql:mysql-connector-j:9.1.0')

	// JSP support for embedded Tomcat (e.g., Docker)
	providedCompile('org.apache.tomcat.embed:tomcat-embed-jasper:11.0.13')
	implementation("jakarta.el:jakarta.el-api:6.0.1")

	// https://docs.spring.io/spring-boot/docs/current/reference/htmlsingle/#howto-create-a-deployable-war-file
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

	integrationTestImplementation 'org.testcontainers:testcontainers-mysql:2.0.1'
	integrationTestImplementation 'org.testcontainers:testcontainers-junit-jupiter:2.0.1'
	integrationTestImplementation 'com.mysql:mysql-connector-j:9.1.0'
}

def uiDir = 'src/client3'
def uiDistDir = "${uiDir}/dist"

node {
	version = '20.19.0'
	npmVersion = '10.5.0'
	download = true
	// distBaseUrl = Custom artifactory location here for node/npm.
}

clean.doFirst {
	delete("$buildDir/frontend")
}

/*
Running npm install before npm run build ensures all required dependencies are downloaded and available.
This prevents build failures due to missing packages, guaranteeing that your build uses the correct versions specified in package.json.
*/
tasks.register('npmInstallBloggerUI', NpmTask) {
	args = ['install']
	execOverrides {
		it.workingDir = uiDir
	}
}

tasks.register('npmBuildBloggerUI', NpmTask) {
	dependsOn npmInstallBloggerUI
	args = ['run','build']

	// Tell Gradle when to rerun this task
	inputs.dir("$uiDir/src") // Vue source files
	inputs.file("$uiDir/package.json")
	inputs.file("$uiDir/package-lock.json")
	outputs.dir(uiDistDir)  // Output directory

	execOverrides {
		it.workingDir = uiDir
	}
}

//tasks.named('resolveMainClassName') {
//	dependsOn tasks.copyUiToStatic
//}

// for embedded Tomcat in Spring Boot
tasks.register('copyUiToStatic', Copy) {
	dependsOn npmBuildBloggerUI
	from uiDistDir
	into "$buildDir/resources/main/static/tb-ui" // Spring Boot static location
}

// for external Tomcat deployment
// serves content from root of WAR
war {
	enabled = true
	dependsOn npmBuildBloggerUI
	from uiDistDir into "" // root of WAR for external Tomcat
}

// for embedded Tomcat in Docker
// serves content from /static in classpath
bootWar {
	enabled = true
	dependsOn copyUiToStatic
}

test {
    useJUnitPlatform()
}

tasks.processResources.dependsOn(tasks.copyUiToStatic)
